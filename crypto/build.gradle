plugins {
    id "java-library"
    id 'jacoco'
    id "java-test-fixtures"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group 'org.cryptimeleon.incentive'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.cryptimeleon:craco:4.0.1'
    api 'org.cryptimeleon:math:3.1.1'
    implementation 'org.cryptimeleon:mclwrap:3.2.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")

    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    exclude '**/benchmark/run/**'
}

def benchmark = sourceSets.create('benchmark')

configurations[benchmark.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[benchmark.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)


def benchmarkTask = tasks.register('benchmark', Test) {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
    include '**/benchmark/run/**'
}
