plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt' // Without this databinding does not work correctly
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}


android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.cryptimeleon.incentive.app"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        compose true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    applicationVariants.all { variant ->
        variant.getRuntimeConfiguration().exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    composeOptions {
        kotlinCompilerExtensionVersion rootProject.composeVersion
    }
    // Common test classes
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    implementation 'androidx.test.ext:junit-ktx:1.1.3'
    implementation 'androidx.test:core-ktx:1.3.0'
    def room_version = "2.4.0-rc01"
    def activity_version = "1.4.0"
    def fragment_version = "1.4.0"
    def nav_version = "2.4.0-alpha02"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Support for activityViewModel to allow shared view models
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // Room database
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt("androidx.room:room-compiler:$room_version")

    // Run coroutines in view model scope/ lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    // Live data
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'

    // Cryptimeleon
    implementation 'org.cryptimeleon.incentive:crypto:1.0.0'
    implementation 'org.cryptimeleon.incentive:promotion:1.0.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Math / Statistics
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // JSON
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'


    // ML barcode scanning
    implementation 'com.google.mlkit:barcode-scanning:17.0.0'

    // CameraX dependencies for scanner
    implementation "androidx.camera:camera-camera2:1.1.0-alpha11"
    implementation "androidx.camera:camera-lifecycle:1.1.0-alpha11"
    implementation "androidx.camera:camera-view:1.0.0-alpha31"

    // https://stackoverflow.com/questions/56639529/duplicate-class-com-google-common-util-concurrent-listenablefuture-found-in-modu
    //implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.google.guava:guava:30.0-android'

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Jetpack Compose
    implementation "androidx.compose.runtime:runtime:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$rootProject.composeVersion"
    implementation "androidx.compose.material:material:$rootProject.composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$rootProject.composeVersion"

    // implementation "com.google.android.material:compose-org.cryptimeleon.incentive.app.theme-adapter:$rootProject.composeVersion"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0-beta01"

    // Activity with compose
    implementation "androidx.activity:activity-compose:$activity_version"

    // Icons
    implementation "androidx.compose.material:material-icons-extended:$rootProject.composeVersion"

    // Permissions
    implementation "com.google.accompanist:accompanist-permissions:0.16.1"

    // accompanist
    implementation "com.google.accompanist:accompanist-insets:0.20.3"
    implementation "com.google.accompanist:accompanist-insets-ui:0.20.3"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.20.3"


    // Testing
    testImplementation 'junit:junit:4.13.2'
    // Mocking
    testImplementation("org.mockito:mockito-core:3.6.28")
    androidTestImplementation("org.mockito:mockito-core:3.6.28")
    // Nicer assertions, requires Guava!
    testImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation "com.google.truth:truth:1.1.3"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
